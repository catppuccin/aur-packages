# Maintainer: Catppuccin <releases@catppuccin.com>

_flavor=macchiato
pkgname=catppuccin-cursors-$_flavor
pkgver=2.0.0 # renovate: datasource=github-tags depName=catppuccin/cursors
pkgrel=2
pkgdesc="Soothing pastel mouse cursors - ${_flavor^}"
arch=('any')
license=('GPL2')
url='https://github.com/catppuccin/cursors'

source=()
sha256sums=('4c66fba0272661f03dee2e5ffefd1829e1a94625781237804bc791ca5a781c42'
            '206ea3995f859baef5f4f7bd2669ecd16a60d70d9d4241b7eea7a89b0dde7572'
            'a51490a997e95572c7e00ba128435296e0285e013cdf6db7a600488fff2df7e3'
            'c79852bcbd8a82402ef2d8fdffd54ef959eeb6d3e0675512985bf0f22a6832bc'
            '6f5a5f71ab0ca9ed088dca1167b7fd8b4921c16234fc073d15fce1c54965eeae'
            'a1d55b07dddfca4678cb300b5b75618de66d730cc22cedaba1ed6f2952a7db1c'
            'a0349e9ef2ee46afd245026bc520849b5bd36b7618b38c1a5fe807b8563a62ed'
            'e7388debd7694c0da59aaa1d4f37d98517ff9cf231d15b9540e941db3543c1d9'
            'ba60a5cf8829267585b46fa82594384f231af258eee4d541972d14887ee7b88a'
            '47e452cbc8f7d6cb40249eef8d442960aefef2ac0b977352e03065397e8e8444'
            '1a35badf6a2c35408a632007ddebdf00db45b54edbe7a4928b99e643ad838b7e'
            '7dad1841a8585f95ecf79f977eb64642ca8c422efdd770d66ec74fab23d655d8'
            '34e3bfee2421c9d87ef63a96deb574d2a38bd905553f90aedf725059811a00a8'
            'c04cb5dcc5672a23b4099fddb71a79927e0d9bb07c4ac965fb30433df12ad8c1'
            'b4a1ef760769503a0b31a3fedd6c8928c7cc3eed3274006a0be6b2b533c7e8ec'
            'c354b397797df5b4fd0868b53e7d4e8f64899ef8ca41a264a9dc64ed6a975e8f')

_colors_all=(blue dark flamingo green lavender light maroon mauve peach pink red rosewater sapphire sky teal yellow)

declare -A _color_index
for i in "${!_colors_all[@]}"; do
    _color_index["${_colors_all[$i]}"]=${sha256sums[$i]}
done

# User can define env CTP_CURSOR_COLOR="mauve dark light" etc
_colors=(${CTP_CURSOR_COLOR:-${_colors_all[@]}})

_active_sha256sums=()

for color in "${_colors[@]}"; do
	if [[ -z ${_color_index[$color]} ]]; then
		echo "Unknown color: '$color', exiting..." >&2
		exit 1
	fi
    source+=("$pkgname-$pkgver-$color.zip::$url/releases/download/v$pkgver/catppuccin-$_flavor-$color-cursors.zip")
    _active_sha256sums+=("${_color_index[$color]}")
done

# updpkgsums: wipes this out, ensure to restore it after running
sha256sums=("${_active_sha256sums[@]}")

package() {
	if [[ -z "$CTP_CURSOR_COLOR" ]]; then
		echo "Installing all cursor colors as CTP_CURSOR_COLOR isn't set..."
	fi

	install -d "$pkgdir/usr/share/icons/"
	cp -r -a --no-preserve=ownership catppuccin-$_flavor-* "$pkgdir/usr/share/icons"
}
