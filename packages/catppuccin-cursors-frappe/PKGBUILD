# Maintainer: Catppuccin <releases@catppuccin.com>

_flavor=frappe
pkgname=catppuccin-cursors-$_flavor
pkgver=2.0.0 # renovate: datasource=github-tags depName=catppuccin/cursors
pkgrel=2
pkgdesc="Soothing pastel mouse cursors - ${_flavor^}"
arch=('any')
license=('GPL2')
url='https://github.com/catppuccin/cursors'

source=()
sha256sums=('6133983b33e07b3dc50b3a833a2a2f9fb45af4fb16b39ad13c8b78381f7c9e9c'
            'a3eaea75b6c8fd6908716b5e8bc769da581f2246a39a7d1a1f9d5c1ee4d72608'
            '57890702ffc137e88efc18ba5c53a6a40b4b096b346cbcbc35c34a532bc78382'
            '5df660a8e6758ec0d6834c02ee196d3c6be20b1396c9e5ca21bccc8a1650f0ad'
            '291623213f96a1b2bfabb5314d6136ab3807793621438b141aaf9a22e5ac919f'
            'c0df379cb2413c38451b0f990a42879489ef85dac2e108498802f7032d2bfd7e'
            'c0f2d4e1191eb4b09a7667b3d7932a0c5fb01b69c730d6a35866f669a4d5f99a'
            'f901e7e6cdd95ff4d9990373f96208527ef1d9fc8f864004d634c2b8723b1e73'
            '623bddd35efc2c8aa4ef69df81bb4360a464bdd5b54d418416ddf5817eceb762'
            'ebf5272b4bfadf65cd2330a0cad30797ca222f216ba88282867a0e2cf458f85e'
            'e1f743b315e5dbd71d410f9b4274d148acfe5591b7532be951cfa67b0e6caa95'
            '93d9e8d9625e732e95890f48ba2635e446dd1d9fe01277c40c0c7fc15809ddea'
            '9aad5b714cacfaf397d325f909fe1b628c6d2f9a592e54480bb65f68b8ce2cd8'
            '4a2dd61441ebf8d746623521334680af58a5896b812d455fd2e8a6a94a9663d5'
            '07b3fb248bebbfbfabc1259d4b318739b3adf25696eb76dd7302616d32de76c0'
            '164c8e8c19e7e296be808a5b3cf89d0bb3cf100e5739ffd41a98539f6777b4de')

_colors_all=(blue dark flamingo green lavender light maroon mauve peach pink red rosewater sapphire sky teal yellow)

declare -A _color_index
for i in "${!_colors_all[@]}"; do
    _color_index["${_colors_all[$i]}"]=${sha256sums[$i]}
done

# User can define env CTP_CURSOR_COLOR="mauve dark light" etc
_colors=(${CTP_CURSOR_COLOR:-${_colors_all[@]}})

_active_sha256sums=()

for color in "${_colors[@]}"; do
	if [[ -z ${_color_index[$color]} ]]; then
		echo "Unknown color: '$color', exiting..." >&2
		exit 1
	fi
    source+=("$pkgname-$pkgver-$color.zip::$url/releases/download/v$pkgver/catppuccin-$_flavor-$color-cursors.zip")
    _active_sha256sums+=("${_color_index[$color]}")
done

# updpkgsums: wipes this out, ensure to restore it after running
sha256sums=("${_active_sha256sums[@]}")

package() {
	if [[ -z "$CTP_CURSOR_COLOR" ]]; then
		echo "Installing all cursor colors as CTP_CURSOR_COLOR isn't set..."
	fi

	install -d "$pkgdir/usr/share/icons/"
	cp -r -a --no-preserve=ownership catppuccin-$_flavor-* "$pkgdir/usr/share/icons"
}
